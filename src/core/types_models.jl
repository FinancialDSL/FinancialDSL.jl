
"""
    PricingModel{C<:Currencies.Currency}

Represents a Pricing Model for a `Contract`.

# Interface

* `get_market_data_provider(model) :: MarketData.AbstractMarketDataProvider`

# Provides

* `get_functional_currency(model) :: Currencies.Currency`

"""
abstract type PricingModel{C<:Currencies.Currency} end

@inline function get_functional_currency(m::PricingModel{C}) :: C where {C<:Currencies.Currency}
    return C()
end

"""
    StaticHedgingModel

Standard discounted cashflow model. Used to price instruments that can be hedged with a static strategy.
"""
struct StaticHedgingModel{C, P<:MarketData.AbstractMarketDataProvider} <: PricingModel{C}
    functional_currency::C
    provider::P
    riskfree_curve_map::Dict{String, Dict{Symbol, Symbol}} # "onshore" -> [ :BRL -> :PRE ]
end

function StaticHedgingModel(currency::Currencies.Currency, riskfree_curve_map::Dict{String, Dict{Symbol, Symbol}})
    return StaticHedgingModel(currency, MarketData.EmptyMarketDataProvider(), riskfree_curve_map)
end

get_market_data_provider(m::StaticHedgingModel) = m.provider

"""
    FutureValueModel

Wrapps a `StaticHedgingModel` and changes its behavior
to disable the discounted cashflow rule,
preserving any projection to future values.

Used to project cashflows.
"""
struct FutureValueModel{C, P<:MarketData.AbstractMarketDataProvider} <: PricingModel{C}
    underlying_model::StaticHedgingModel{C, P}
end

get_market_data_provider(m::FutureValueModel) = get_market_data_provider(m.underlying_model)

# docstrings generated by docs.jl
struct BlackScholesModel{C, P<:MarketData.AbstractMarketDataProvider} <: PricingModel{C}
    static_model::StaticHedgingModel{C, P}
end

struct BinomialModelDaily{C, P<:MarketData.AbstractMarketDataProvider, U<:CashRiskFactor, T<:InterestRates.DayCountConvention} <: PricingModel{C}
    static_model::StaticHedgingModel{C, P}
    underlying::U # CashRiskFactor -> Stock or SpotCurrency
    numeraire_daycount_convention::T # the numeraire is static_model.functional_currency
end
